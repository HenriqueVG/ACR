dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/sp_id_t.c)
AM_INIT_AUTOMAKE(songprint, 1.1)
AM_CONFIG_HEADER(config.h)
AM_ACLOCAL_INCLUDE('macros')

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

AC_CHECK_HEADERS(signal.h fcntl.h sys/resource.h sys/socket.h)
AC_CHECK_HEADERS(netinet/in.h netdb.h errno.h)
AC_CHECK_FUNCS(sigaction signal)

AM_PROG_LIBTOOL

AC_ARG_ENABLE(wav,
[  --disable-wav           turn off wave codec in reference code],,
enable_wav="yes")
AM_CONDITIONAL(HAVE_WAV, test "x$enable_wav" = "xyes")

AC_ARG_ENABLE(mp3,
[  --disable-mp3           turn off mp3 codec in reference code],,
enable_mp3="yes")
AM_CONDITIONAL(HAVE_MPG123, test "x$enable_mp3" = "xyes")

AC_ARG_ENABLE(oggvorbis,
[  --disable-oggvorbis     turn off OggVorbis codec in reference code],,
enable_oggvorbis="yes")
AM_CONDITIONAL(HAVE_OGGVORBIS, test "x$enable_oggvorbis" = "xyes")

dnl Check for libraries

AC_CHECK_LIB(m, cos, MATH_LIB="-lm", MATH_LIB="")
AC_CHECK_LIB(rfftw, rfftw_one, , AC_MSG_ERROR(Cannot find rfftw libraries), -lfftw $MATH_LIB)
AC_CHECK_LIB(fftw, fftw_one, , AC_MSG_ERROR(Cannot find fftw libraries), $MATH_LIB)
AC_CHECK_LIB(xnet, inet_addr)
AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(nsl, gethostbyname)

AM_PATH_GLIB(1.2.0, ,
            AC_MSG_ERROR(Cannot find GLib: is glib-config in path?))

dnl Add the languages which your application supports here.
ALL_LINGUAS=""
AM_GNU_GETTEXT

dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl Set PACKAGE_DATA_DIR in config.h.
if test "x${datadir}" = 'x${prefix}/share'; then
  if test "x${prefix}" = "xNONE"; then
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${ac_default_prefix}/share/${PACKAGE}")
  else
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${prefix}/share/${PACKAGE}")
  fi
else
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${datadir}/${PACKAGE}")
fi

dnl Set PACKAGE_SOURCE_DIR in config.h.
packagesrcdir=`cd $srcdir && pwd`
AC_DEFINE_UNQUOTED(PACKAGE_SOURCE_DIR, "${packagesrcdir}")

dnl Check for various codecs
CODEC_LIBS=
if test "x$enable_wav" = "xyes"; then
    AC_DEFINE(HAVE_WAV)
    CODEC_LIBS='$(top_builddir)'"/src/codecs/wav/libwav_iface.la $CODEC_LIBS"
fi
if test "x$enable_mp3" = "xyes"; then
    AC_DEFINE(HAVE_MPG123)
    CODEC_LIBS='$(top_builddir)'"/src/codecs/mpg123/libmpg123_iface.la $MATH_LIB $CODEC_LIBS"
fi
if test "x$enable_oggvorbis" = "xyes"; then
    AC_DEFINE(HAVE_OGGVORBIS)
    AC_CONFIG_SUBDIRS(src/codecs/vorbis/vorbis)
    CODEC_LIBS='$(top_builddir)'"/src/codecs/vorbis/libvorbis_iface.la $MATH_LIB $CODEC_LIBS"
fi
AC_SUBST(CODEC_LIBS)

dnl this is a test to see if int64_t is defined
dnl this is because AC_CHECK_TYPE breaks on "long long"

AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(long long)

case 8 in
    $ac_cv_sizeof_int) SIZE64="int";;
    $ac_cv_sizeof_long) SIZE64="long";;
    $ac_cv_sizeof_long_long) SIZE64="long long";;
esac

if test -z "$SIZE64"; then
    AC_MSG_WARN(No 64 bit type found on this platform!)
fi

AC_MSG_CHECKING(for int64_t)
AC_CACHE_VAL(has_int64_t,
[AC_TRY_RUN([
#include <sys/types.h>
int64_t foo;
int main() {return 0;}
],
has_int64_t=yes,
has_int64_t=no,
has_int64_t=no
)])
AC_MSG_RESULT($has_int64_t)

if test x$has_int64_t = "xno" ; then
AC_DEFINE_UNQUOTED(int64_t, $SIZE64)
fi

dnl done checking for int64_t


dnl Use -Wall if we have gcc.
changequote(,)
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])

AC_OUTPUT([
Makefile
macros/Makefile
intl/Makefile
po/Makefile.in
src/Makefile
src/codecs/Makefile
src/codecs/mpg123/Makefile
src/codecs/wav/Makefile
src/codecs/vorbis/Makefile
example/Makefile
Win32/Makefile
Win32/GUI/Makefile
Win32/GUI/RES/Makefile
])

